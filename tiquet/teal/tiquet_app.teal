#pragma version 4

//////////////////////////
// Application Creation //
//////////////////////////

// Check txn is for application creation.
txn ApplicationID
int 0
==
// If yes, create tiquet. 
bnz tiquet_creation
// Otherwise, check txn is for opting-in to application. 
txn OnCompletion
int OptIn
==
bnz optin
// Otherwise, check txn is making an application call. 
txn OnCompletion
int NoOp
==
bnz tiquet
// Otherwise, return false.
// TODO: Handle other application ops.
int 0
return
b end_tiquet_app

/////////////////////
// Tiquet Creation //
/////////////////////

tiquet_creation:
// Set tiquet price.
// TODO: Represent price in USD.
byte "PRICE"
int {{TIQUET_PRICE}}
app_global_put
byte "FOR_SALE"
int 1
app_global_put
int 1
return
b end_tiquet_app

/////////////
// Opt-In  //
/////////////

optin:
int 1
return
b end_tiquet_app

/////////////////////////
// Update Application  //
/////////////////////////

update_app:
// Only the application creator can update.
global CreatorAddress
txn Sender
==
return
b end_tiquet_app

//////////////////
// Application  //
//////////////////

// TODO: Activate different code paths via commands specified via an application
// arg
tiquet:
txn NumAppArgs
int 2
==
bnz post_for_resale
txn NumAppArgs
int 1
==
bnz store_escrow_address
b initial_sale
// Fail otherwise.
int 0
return
b end_tiquet_app

store_escrow_address:
byte "ESCROW_ADDRESS"
txna ApplicationArgs 0
app_global_put
int 1
return
b end_tiquet_app

///////////////////
// Initial Sale  //
///////////////////

initial_sale:

global GroupSize
int 4
>=
gtxn 0 TypeEnum
int appl
==
&&
gtxn 1 TypeEnum
int axfer
==
&&
byte "ESCROW_ADDRESS"
app_global_get
gtxn 1 Sender
==
&&
gtxn 2 TypeEnum
int pay
==
&&
gtxn 2 Receiver
int {{TIQUET_ID}}
asset_holding_get AssetBalance
store 0
store 1
load 0
&&
byte "PRICE"
app_global_get
gtxn 2 Amount
==
&&
gtxn 3 TypeEnum
int pay
==
&&
store 0
global GroupSize
int 4
==
bnz finish_sale
global GroupSize
int 5
==
bnz finish_resale
b failure

finish_resale:
load 0
gtxn 4 TypeEnum
int pay
==
&&
store 0
b finish_sale

finish_sale:
load 0
byte "FOR_SALE"
int 0
app_global_put
bnz success
b failure

/////////////////////
// Post for Resale //
/////////////////////

post_for_resale:
// 0th-index value into accounts array, i.e. caller's address. 
int 0
int {{TIQUET_ID}}
asset_holding_get AssetBalance
store 0
store 1
load 0
// Fail if the caller doesn't own the TASA.
bz failure
byte "FOR_SALE"
int 1
app_global_put
byte "PRICE"
txna ApplicationArgs 1
btoi
app_global_put
b success

/////////////////////
// Application End //
/////////////////////

success:
int 1
return
b end_tiquet_app

failure:
int 0
return
b end_tiquet_app

end_tiquet_app:
